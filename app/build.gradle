apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.devtools.ksp'

ksp {
    arg("room.schemaLocation", "$projectDir/schemas".toString())
}

android {
    compileSdkVersion 34

    defaultConfig {
        applicationId "klalumiere.repertoire"
        minSdkVersion 22
        targetSdkVersion 34
        versionCode 21
        versionName "2.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file("$System.env.REPERTOIRE_STORE_FILE_PATH")
            storePassword "$System.env.REPERTOIRE_STORE_PASSWORD"
            keyAlias "$System.env.REPERTOIRE_KEY_ALIAS"
            keyPassword "$System.env.REPERTOIRE_KEY_PASSWORD"
        }
    }

    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/AL2.0', 'META-INF/LGPL2.1']
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
    namespace 'klalumiere.repertoire'
    lint {
        disable 'ObsoleteLintCustomCheck'
    }
}

dependencies {
    def espresso_version = "3.5.1"
    def lifecycle_version = "2.8.2"
    def room_version = "2.6.1"

    annotationProcessor("com.google.dagger:dagger-compiler:2.51.1")
    ksp("com.google.dagger:dagger-compiler:2.51.1")
    ksp "androidx.room:room-compiler:$room_version"
    androidTestImplementation "android.arch.core:core-testing:1.1.1"
    androidTestImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test:core-ktx:1.5.0"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3"
    implementation "androidx.activity:activity-ktx:1.9.0"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.fragment:fragment-ktx:1.8.0" // Prevent crash when adding content
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.8.2"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // Updating `recyclerview-selection` to version `1.1.0` breaks `espresso`'s `longClick`,
    // which is used in `EndToEndTests.kt`. Hence, we don't update it for now. See issue at
    // https://issuetracker.google.com/issues/188992115
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "com.google.android.material:material:1.12.0" // content_main.xml ConstraintLayout
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "android.arch.core:core-testing:1.1.1"
    testImplementation "androidx.lifecycle:lifecycle-runtime-testing:2.8.2"
    testImplementation "androidx.test.ext:junit:1.1.5"
    testImplementation "androidx.test:core-ktx:1.5.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3"
    testImplementation "org.robolectric:robolectric:4.4"

    implementation fileTree(dir: "libs", include: ["*.jar"])
}

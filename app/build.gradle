apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "klalumiere.repertoire"
        minSdkVersion 22
        targetSdkVersion 30
        versionCode 21
        versionName "2.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental": "true"]
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        release {
            storeFile file("$System.env.REPERTOIRE_STORE_FILE_PATH")
            storePassword "$System.env.REPERTOIRE_STORE_PASSWORD"
            keyAlias "$System.env.REPERTOIRE_KEY_ALIAS"
            keyPassword "$System.env.REPERTOIRE_KEY_PASSWORD"
        }
    }

    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lintOptions {
        disable "ObsoleteLintCustomCheck"
    }
    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

dependencies {
    def espresso_version = "3.3.0"
    def lifecycle_version = "2.2.0"
    def room_version = "2.4.0-alpha02"

    androidTestImplementation "android.arch.core:core-testing:1.1.1"
    androidTestImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test:core-ktx:1.3.0"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.9"
    implementation "androidx.activity:activity-ktx:1.2.3"
    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "androidx.fragment:fragment-ktx:1.3.4" // Prevent crash when adding content
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // Updating `recyclerview-selection` to version `1.1.0` breaks `espresso`'s `longClick`,
    // which is used in `EndToEndTests.kt`. Hence, we don't update it for now. See issue at
    // https://issuetracker.google.com/issues/188992115
    implementation "androidx.recyclerview:recyclerview-selection:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.2.0"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "com.google.android.material:material:1.3.0" // content_main.xml ConstraintLayout
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "android.arch.core:core-testing:1.1.1"
    testImplementation "androidx.lifecycle:lifecycle-runtime-testing:2.3.1"
    testImplementation "androidx.test.ext:junit:1.1.2"
    testImplementation "androidx.test:core-ktx:1.3.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "junit:junit:4.12"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.9"
    testImplementation "org.robolectric:robolectric:4.4"

    implementation fileTree(dir: "libs", include: ["*.jar"])
}
